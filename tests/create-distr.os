#Использовать asserts
#Использовать "../src"

Функция ПолучитьСписокТестов(Тестирование) Экспорт
    
    Список = Новый Массив;
    Список.Добавить("Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища");
    Список.Добавить("Тест_ДолженПроверитьЧтоПараметрыКомСтрокиПолученияВерсииРазобраны");
    
    Возврат Список;
    
КонецФункции

Процедура Тест_ДолженПроверитьЧтоПараметрыКомСтрокиПолученияВерсииРазобраны() Экспорт
    
    ПараметрыКомСтроки = Новый Соответствие;
    ПараметрыКомСтроки["АдресХранилища"] = "/some/path/storage";
    ПараметрыКомСтроки["КаталогСборки"] = "/some/path/buildRoot";
    ПараметрыКомСтроки["ВерсияКонфигурации"] = "1.0.4.1";
    ПараметрыКомСтроки["-storage-user"] = "Администратор";
    ПараметрыКомСтроки["-storage-pwd"]  = "123";
    ПараметрыКомСтроки["-storage-v"]    = "4";
    ПараметрыКомСтроки["-cfu-basedir"]  = "/some/path/basedir";
    ПараметрыКомСтроки["-update-from"]  = "1.0.2.1, 1.0.3.7";
    
    Команда = Новый КомандаСобратьИзХранилища;
    
    Параметры = Команда.РазобратьПараметры(ПараметрыКомСтроки);
    
    Ожидаем.Что(Параметры.АдресХранилища).Равно("/some/path/storage");
    Ожидаем.Что(Параметры.КаталогСборки).Равно("/some/path/buildRoot");
    Ожидаем.Что(Параметры.ВерсияКонфигурации).Равно("1.0.4.1");
    Ожидаем.Что(Параметры.ПользовательХранилища).Равно("Администратор");
    Ожидаем.Что(Параметры.ПарольХранилища).Равно("123");
    Ожидаем.Что(Параметры.ВерсияХранилища).Равно("4");
    Ожидаем.Что(Параметры.КаталогВерсий).Равно("/some/path/basedir");
    Ожидаем.Что(Параметры.ПредыдущиеВерсии).ИмеетТип("Массив");
    Ожидаем.Что(Параметры.ПредыдущиеВерсии[0]).Равно("1.0.2.1");
    Ожидаем.Что(Параметры.ПредыдущиеВерсии[1]).Равно("1.0.3.7");
    
КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища() Экспорт
    
    Команда = Новый КомандаСобратьИзХранилища();
    
    ФайлХранилища = ТекущийСценарий().Каталог + "/fixtures/storage.1CD";
    ВремКаталогХранилища = ПолучитьИмяВременногоФайла();
    СоздатьКаталог(ВремКаталогХранилища);
    ВремФайлКонфигурации = ПолучитьИмяВременногоФайла("1CD");
    КопироватьФайл(ФайлХранилища, ВремКаталогХранилища + "/1cv8ddb.1CD");
    
    Попытка
        Команда.ВыгрузитьВерсиюИзХранилища(ВремКаталогХранилища, 2, ВремФайлКонфигурации, "Администратор");
        ФайлТест = Новый Файл(ВремФайлКонфигурации);
        Ожидаем.Что(ФайлТест.Существует(), "файл конфигурации должен существовать"); 
    Исключение
        УдалитьФайлы(ВремКаталогХранилища);
        УдалитьФайлы(ВремФайлКонфигурации);
        ВызватьИсключение;
    КонецПопытки;
    
    УдалитьФайлы(ВремКаталогХранилища);
    УдалитьФайлы(ВремФайлКонфигурации);
    
КонецПроцедуры // Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища()
    