
Перем ФайлМанифеста Экспорт;
Перем СоздаватьИнсталлятор Экспорт;
Перем СоздаватьФайлыПоставки Экспорт;

Функция Собрать(Знач УправлениеКонфигуратором, Знач ВерсияМетаданных, Знач ИмяКаталогаШаблонаВерсии) Экспорт
    
    Манифест = ПолучитьМанифестСборки(УправлениеКонфигуратором.КаталогСборки());
    УстановитьНомерСборкиВМанифесте(Манифест, ВерсияМетаданных, ИмяКаталогаШаблонаВерсии);
    
    ВыходнойКаталог = Новый Файл(УправлениеКонфигуратором.КаталогСборки() + "\distr");
	Если Не ВыходнойКаталог.Существует() Тогда
		СоздатьКаталог(ВыходнойКаталог.ПолноеИмя);
	ИначеЕсли ВыходнойКаталог.ЭтоКаталог() Тогда
		УдалитьФайлы(ВыходнойКаталог.ПолноеИмя, "*.*");
	Иначе
		ВызватьИсключение "Каталог <" +ВыходнойКаталог.ПолноеИмя+ "> не является каталогом";
	КонецЕсли;
	
	// внешние файлы релиза
	СИ = Новый СистемнаяИнформация();
	СИ.УстановитьПеременнуюСреды("V8BuildRoot", УправлениеКонфигуратором.КаталогСборки());
    
    Параметры = УправлениеКонфигуратором.ПолучитьПараметрыЗапуска();
	Параметры.Добавить("/CreateDistributive """ + ВыходнойКаталог.ПолноеИмя + """");
	Параметры.Добавить("-File """ + ФайлМанифеста + """");
	Если СоздаватьФайлыПоставки Тогда
        Параметры.Добавить("-MakeFiles");
    КонецЕсли;
    Если СоздаватьИнсталлятор Тогда
        Параметры.Добавить("-MakeInstall");
    КонецЕсли;
	
	УправлениеКонфигуратором.ВыполнитьКоманду(Параметры);
	Сообщить(УправлениеКонфигуратором.ВыводКоманды());
	
	Возврат ВыходнойКаталог.ПолноеИмя;
    
КонецФункции // Собрать()

Функция ПолучитьМанифестСборки(Знач КаталогСборки)

	ИсходныйМанифест = ФайлМанифеста;
	РабочийМанифест = ОбъединитьПути(КаталогСборки, "package.edf");
	КопироватьФайл(ИсходныйМанифест, РабочийМанифест);
	
	Возврат РабочийМанифест;
	
КонецФункции

Процедура УстановитьНомерСборкиВМанифесте(Знач РабочийФайлМанифеста, Знач ВерсияМетаданных, Знач НомерСборки)

	ЧтениеТекста = Новый ЧтениеТекста(РабочийФайлМанифеста);
	Данные = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	НовыеДанные = СтрЗаменить(Данные, "%НомерСборки%", НомерСборки);
	НовыеДанные = СтрЗаменить(НовыеДанные, "%ВерсияМетаданных%", ВерсияМетаданных);
	
	Запись = Новый ЗаписьТекста(РабочийФайлМанифеста);
	Запись.Записать(НовыеДанные);
	Запись.Закрыть();

КонецПроцедуры

////////////////////////////////////////////////////////////////////

СоздаватьИнсталлятор = Истина;
СоздаватьФайлыПоставки = Ложь;